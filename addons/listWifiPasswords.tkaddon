::toolkitAddon List Wi-fi passwords for all saved networks
netsh wlan show profile > nul
	if "%errorlevel%" == "1" echo This device doesn't seem to have Wi-Fi enabled...& exit /b
	echo Saved Wi-Fi networks:
	FOR /f "tokens=5-15 USEBACKQ" %%A IN (`netsh wlan show profile ^| find ": "`) DO (
		set WiFiName=%%A %%B %%C %%D %%E %%F %%G %%H %%I %%JToolkitNull
		for /l %%M in (1, 1, 11) do (
			set WiFiName=!WiFiName: ToolkitNull=ToolkitNull!
		)
		set WiFiName=!WiFiName:ToolkitNull=!

		set command=netsh wlan show profile "!WiFiName!" key=clear ^| find "SSID name"
		for /f "delims=" %%N in ('!command!') do set "WiFiSSID=%%N"

		set command=netsh wlan show profile "!WiFiName!" key=clear ^| find "Key Content"
		for /f "delims=" %%N in ('!command!') do set "WiFiPassword=%%N"

		set command=netsh wlan show profile "!WiFiName!" key=clear ^| find "Authentication"
		for /f "delims=" %%N in ('!command!') do set "WiFiAuthType=%%N"

		set "WiFiSSID=!WiFiSSID:~30,-1!"
		set "WiFiAuthType=!WiFiAuthType:~29!"
		set "WiFiPassword=!WiFiPassword:~29!"

		echo.
		echo SSID:              !WiFiSSID!
		echo Security type:     !WiFiAuthType!
		echo Password:          !WiFiPassword!
	)